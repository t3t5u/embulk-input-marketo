plugins {
    id "java"
    id "maven-publish"
    id "checkstyle"
    id "jacoco"
    id "com.jfrog.bintray" version "1.8.4"
    id "org.embulk.embulk-plugins" version "0.4.2"
}

repositories {
    mavenCentral()
    jcenter()
}

group = "com.treasuredata.embulk.plugins"
description = "Loads records from Marketo."
version = "0.6.20"

sourceCompatibility = 1.8
targetCompatibility = 1.8
def embulkVersion = '0.10.25'

dependencies {
    compileOnly "org.embulk:embulk-api:$embulkVersion"
    compileOnly "org.embulk:embulk-spi:$embulkVersion"

    compile('org.embulk:embulk-util-config:0.2.1') {
        // Conflict with Embulk Core
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-annotations'
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-core'
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-databind'
        exclude group: 'com.fasterxml.jackson.datatype', module: 'jackson-datatype-jdk8'
        exclude group: 'javax.validation', module: 'validation-api'
    }

    compile "org.embulk:embulk-util-json:0.1.0"
    compile "org.embulk:embulk-util-text:0.1.0"
    compile "org.embulk:embulk-util-timestamp:0.2.1"

    // Explicit dependencies for embulk-util-* that matches with Embulk
    compile 'com.fasterxml.jackson.core:jackson-core:2.6.7'
    compile 'com.fasterxml.jackson.core:jackson-annotations:2.6.7'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.6.7'
    compile 'com.fasterxml.jackson.datatype:jackson-datatype-jdk8:2.6.7'
    compile 'javax.validation:validation-api:1.1.0.Final'

    compile('org.embulk:embulk-base-restclient:0.10.0') {
        exclude group: 'org.msgpack', module: 'msgpack-core'
    }
    compile('org.embulk:embulk-util-retryhelper-jetty92:0.8.1') {
        exclude group: 'org.slf4j', module: 'slf4j-api'
    }

    compile 'com.google.guava:guava:18.0'
    compile  "com.google.code.findbugs:annotations:3.0.1"
    compile 'org.apache.commons:commons-lang3:3.4'

    testCompile "junit:junit:4.+"
    testCompile "org.embulk:embulk-core:$embulkVersion:tests"
    testCompile "org.embulk:embulk-junit4:$embulkVersion"
    testCompile "org.embulk:embulk-deps:$embulkVersion"
    testCompile "org.mockito:mockito-core:2.+"
}

embulkPlugin {
    mainClass = "org.embulk.input.marketo.MarketoInputPlugin"
    category = "input"
    type = "marketo"
}

gem {
    authors = ["uu59", "yoshihara", "taikhuu", "Treasure Data"]
    email = ["k@uu59.org", "h.yoshihara@everyleaf.com", "tai@treasuredata.com", "dev@treasure-data.com"]
    summary = "Marketo input plugin for Embulk"
    homepage = "https://github.com/treasure-data/embulk-input-marketo"
    licenses = ["Apache2"]
}

javadoc {
    options {
        locale = 'en_US'
        encoding = 'UTF-8'
    }
}

// bintray and publishing
bintray {
    // write at your bintray user name and api key to ~/.gradle/gradle.properties file:
    user = project.hasProperty('bintray_user') ? bintray_user : ''
    key = project.hasProperty('bintray_api_key') ? bintray_api_key : ''

    publications = [ "embulkMavenRelease" ]
    publish = true

    pkg {
        userOrg = 'embulk-input-marketo'
        repo = 'maven'
        name = project.name
        desc = 'Marketo input plugin for Embulk'
        websiteUrl = 'https://github.com/treasure-data/embulk-input-marketo'
        issueTrackerUrl = 'https://github.com/treasure-data/embulk-input-marketo/issues'
        vcsUrl = 'https://github.com/treasure-data/embulk-input-marketo.git'
        licenses = ['Apache2']
        labels = ['embulk', 'java']
        publicDownloadNumbers = true

        version {
            name = project.version
        }
    }
}

publishing {
    publications {
        embulkPluginMaven(MavenPublication) {  // Publish it with "publishEmbulkPluginMavenPublicationToMavenRepository".
            from components.java  // Must be "components.java". The dependency modification works only for it.
        }
    }
    repositories {
        maven {
            url = uri("https://rubygems.org")
        }
    }
}

checkstyle {
    configFile = file("${project.rootDir}/config/checkstyle/checkstyle.xml")
    toolVersion = '6.14.1'
}
checkstyleMain {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = true
}
checkstyleTest {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = true
}
task checkstyle(type: Checkstyle) {
    classpath = sourceSets.main.output + sourceSets.test.output
    source = sourceSets.main.allJava + sourceSets.test.allJava
}

jacocoTestReport {
    group = "Reporting"
    reports {
        xml.enabled false
        csv.enabled false
        html.destination file("${buildDir}/reports/coverage")
    }
}

gemPush {
    host = "https://rubygems.org"
    outputs.upToDateWhen { false }
}
